group = "com.arthenica.ffmpegkit.flutter"
version = "1.0-SNAPSHOT"
apply from: './verify_repo_paths.gradle'
buildscript {
    ext.kotlin_version = "2.1.10"
    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        classpath("com.android.tools.build:gradle:8.3.0")
        classpath("org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version")
    }
}

rootProject.allprojects {
    repositories {
        // 添加本地仓库引用
        maven {
            url = uri("file://${rootProject.projectDir}/aar/repo")
        }
        google()
        mavenCentral()
        mavenLocal()
    }
}

apply plugin: "com.android.library"
apply plugin: "kotlin-android"
apply plugin: 'maven-publish'
publishing {
    publications {
        aar(MavenPublication) {
            groupId = 'com.arthenica666'
            artifactId = 'ffmpeg-kit-full-gpl'
            version = '6.0-2.LTS'
            // 指定本地 .aar 文件的路径
            artifact("libs/ffmpeg-kit-full-gpl-6.0-2.LTS.aar")
        }
    }
    repositories {
        maven {
            url = uri("file://${rootProject.projectDir}/aar/repo")
        }
    }
}
android {
    namespace = "com.arthenica.ffmpegkit.flutter"

    compileSdk = 35

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_17.toString()
    }

    buildTypes {
        release {
            minifyEnabled false
        }
    }

    sourceSets {
        main.java.srcDirs += "src/main/kotlin"
    }

    defaultConfig {
        minSdk = 24
        versionCode 603
        versionName "6.0.3"
    }

    dependencies {
        implementation("androidx.annotation:annotation:1.9.1")
        implementation("com.arthenica666:ffmpeg-kit-full-gpl:6.0-2.LTS")
    }
}
tasks.named("build").configure {
    finalizedBy("publish")
}